plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.github.triplet.play' version '2.8.1'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

def keystoreProperties = new Properties()
def keystorePropertiesFile = file('keystore.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    def versionName = stdout.toString().trim()
    if (project.gradle.startParameter.taskNames.any { it.toLowerCase().contains('release') } && versionName.contains('-')) {
        throw new Exception("Invalid version name: $versionName")
    }
    return versionName
}

def getPreviousVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--abbrev=0', '@^'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getChangelog = { ->
//    git log --oneline $(git describe --tags --abbrev=0 @^)..@
    def previousVersion = getPreviousVersion()
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'log', '--pretty=%s', '--reverse', "$previousVersion..@"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

task generateChangelog {
    new File("$projectDir/src/main/play/release-notes/en-US", "default.txt").text = getChangelog()
}
tasks.publish.dependsOn(generateChangelog)

android {
    compileSdkVersion 30
    ndkVersion "22.0.7026061"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'

        // TODO Remove this workaround for a bug in the build tools
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId "net.cachapa.tudo"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1  // Will be overridden on release
        versionName getVersionName()
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".beta"
        }

        release {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

play {
    defaultToAppBundles = true
    serviceAccountCredentials = file("play-store.json")
    userFraction = 1
    resolutionStrategy = "auto"
//    track = "beta"
}
